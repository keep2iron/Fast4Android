apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
if (MAVEN_LOCAL.toBoolean()) {
    apply plugin: 'maven'
} else {
    apply plugin: 'com.novoda.bintray-release'
}

android {
    resourcePrefix "fast"

    compileSdkVersion versions.build.target_sdk
    buildToolsVersion versions.build.build_tools

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion versions.build.target_sdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    //support
    implementation deps.kotlin.stdlib_jdk7

    implementation project(':comp-fast4android')
    api project(':base-style')
    implementation deps.support.annotations

    testImplementation deps.test.junit
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}

if (MAVEN_LOCAL.toBoolean()) {
    group = rootProject.ext.version_group
    version = rootProject.ext.version_fast4android_topbarlayout

    uploadArchives {
        repositories {
            mavenDeployer {
                // 唯一标识
                pom.groupId = rootProject.ext.version_group
                // 项目名称
                pom.artifactId = "fast4android-topbarlayout"
                repository(url: uri(project.MAVEN_LOCAL_REPO))
                // 版本号
                pom.version = rootProject.ext.version_fast4android_topbarlayout
            }
        }
    }

// 指定编码
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

// 打包源码
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }

// 制作文档(Javadoc)
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}else {
    publish {
        userOrg = 'keep2iron'
        groupId = rootProject.ext.version_group
        artifactId = 'fast4android-topbarlayout'
        publishVersion = rootProject.ext.version_fast4android_topbarlayout
        desc = 'fast4android'
        website = 'https://keep2iron.github.io'
    }
}